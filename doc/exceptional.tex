% !TEX root =  eMAGma.tex

Magma provides functionality with common exceptional tensors.  Many are used in the
construction of nonassociative algebras.  A few supporting functions for nonassociative algebras
are also provided.

\section{Generics for nonassociative algebras}

\subsection{Nonassociative algebras with involutions}~

\index{IsStarAlgebra}
\begin{intrinsics}
IsStarAlgebra(A) : AlgGen -> BoolElt
\end{intrinsics}

Decides if algebra has an involution, i.e. a $*$-algebra.

\index{Star}
\begin{intrinsics}
Star(A) : AlgGen -> Map
\end{intrinsics}

Returns involution of given $*$-algebra.

\begin{example}[StarAlgebra]

We demonstrate the functions dealing with involutions of nonassociative algebras.
\begin{code}
> A := OctonionAlgebra(Rationals(),-1,-1,-1);
> IsStarAlgebra(A);
true
> 
> s := Star(A);
> A.1; // A.1 is the mult. id.
(1 0 0 0 0 0 0 0)
> A.1 @ s; 
(1 0 0 0 0 0 0 0)
> 
> A.2;
(0 1 0 0 0 0 0 0)
> A.2 @ s;
( 0 -1  0  0  0  0  0  0)
\end{code}
\end{example}

\subsection{Operations on power associative algebras}~
The following operations are defined for nonassociative algebras for which $x*(x*x)=(x*x)*x$.

\index{GenericMinimalPolynomial}\index{GenericMinimumPolynomial}
\begin{intrinsics}
GenericMinimalPolynomial(x) : AlgGenElt -> FldElt
GenericMinimumPolynomial(x) : AlgGenElt -> FldElt
\end{intrinsics}

The generic minimum polynomial of an element in a power associative algebra.

\index{GenericNorm}
\begin{intrinsics}
GenericNorm(x) : AlgGenElt -> FldElt
\end{intrinsics}

The generic norm of an element in a power associative algebra.

\index{GenericTrace}
\begin{intrinsics}
GenericTrace(x) : AlgGenElt -> FldElt
\end{intrinsics}

The generic trace of an element in a power associative algebra.

\index{GenericTracelessSubspaceBasis}
\begin{intrinsics}
GenericTracelessSubspaceBasis(A) : AlgGen -> Any
\end{intrinsics}

Given a power associative algebra return a basis for the elements of generic trace 0.

\begin{example}[TenGeneric]

The trace $x+\bar{x}$ of a quaternion doubles the rational component, producing
degenerate behavior in characteristic $2$.  The generic trace avoids this.
\begin{code}
> Q := QuaternionAlgebra(Rationals(), 1,1);
> Trace(Q!1);        
2
> GenericTrace(Q!1);
1
> Q := QuaternionAlgebra(GF(2), 1,1);  
> Trace(Q!1);
0
> GenericTrace(Q!1);
1
\end{code}

The generic minimum polynomial of an element $x$ in power associative algebra
need only be a factor of the minimal polynomial of its right regular matrix $yR_x:=x*y$.

\begin{code}
> J := ExceptionalJordanCSA(GF(5));
> p := GenericMinimumPolynomial(J.3+J.12);
> Rx := AsMatrices(Tensor(J), 2,0);     // yR_x = y*x.
> q := MinimalPolynomial(Rx[3]+Rx[12]); 
> Degree(p);
3
> Degree(q);
6
> q mod p;
0
\end{code}
\end{example}

\section{Compositions algebras}

\index{CompositionAlgebra}
\begin{intrinsics}
CompositionAlgebra(K, a) : Fld, [FldElt] -> AlgGen
CompositionAlgebra(K, a) : Fld, [RngIntElt] -> AlgGen
\end{intrinsics}

Constructs the composition algebra with specified parameters.  The algebra returned
has an involution.  
The method is modestly intentional choosing Magma's favored representation of
the individually classified algebras according to Hurwitz's theorem.  In the case of
fields the type returned is an algebra with involution, possibly the identity.


\index{OctonionAlgebra}
\begin{intrinsics}
OctonionAlgebra(K, a, b, c) : Fld, FldElt, FldElt, FldElt -> AlgGen
OctonionAlgebra(K, a, b, c) : Fld, RngIntElt, RngIntElt, RngIntElt -> AlgGen
\end{intrinsics}

Octonion algebra with involution given by the specified parameters.
This builds the Cayley-Dickson algebra over the quaternion algebra
$\left(\frac{a,b}{K}\right)$.  In particular, Magma's implementation
of quaternion algebras is applied.

\index{SplitOctonionAlgebra}
\begin{intrinsics}
SplitOctonionAlgebra(K) : Fld -> AlgGen
\end{intrinsics}

Returns the split octonion algebra over the field $F$.


\begin{example}[TenTriality]
The following example demonstrates some of the mechanics by exploring
the concept of triality \cite{Schafer}*{III.8}.

The Cartan-Jacobson theorem asserts that for fields of characteristic other
than 2 and 3, the derivation algebra of an octonion algebra is of Lie type 
$G_2$.

\begin{code}
> O := OctonionAlgebra(GF(7),-1,-1,-1);
> L := DerivationAlgebra(O);   // Derivations as an algebra.
> SemisimpleType(L);
G2
\end{code}

Cartan's triality obtains $G_2$ from $D_4$ by relaxing to
derivations of the octonions as a generic tensor, rather than as an 
algebra.  
This is done computationally by changing the category of the octonion product
from an algebra to a tensor.

\begin{code}
> T := Tensor(O);
> T := ChangeTensorCategory(T,HomotopismCategory(2)); 
> M := DerivationAlgebra(T);  // Derivations as a tensor.
> SemisimpleType(M/SolvableRadical(M));
D4
\end{code}
\end{example}

\section{Jordan algebras}


\index{JordanTripleProduct}
\begin{intrinsics}
JordanTripleProduct(J) : AlgGen -> TenSpcElt
\end{intrinsics}

Returns the tensor describing the Jordan triple product.

\index{JordanSpinAlgebra}
\begin{intrinsics}
JordanSpinAlgebra(F) : TenSpcElt -> AlgGen
JordanSpinAlgebra(F) : Mtrx -> AlgGen
\end{intrinsics}

Returns the special Jordan algebra of spin type for given symmetric form.

\begin{example}[JordanBasic]
Jordan algebras have suggestive analogues of 
commutative associative algebras, but experimenting shows serious 
differences.

\begin{code}
> F := IdentityMatrix(Rationals(),2);
> J := JordanSpinAlgebra(F);
> T := Tensor(J); 
> R := AsMatrices( T, 2,0); 
> R[1];   // Is J.1 the identity?
[1 0 0]
[0 1 0]
[0 0 1]
> J.2*J.2 eq J.1;  // J.2^2=1?
true
> J.2*J.3 eq 0;  // Yet J.2 is a zero-divisor.
true
> e := (1/2)*(J.1+J.2);             
> e^2 eq e;  // An idempotent of J?
true
\end{code}

Pierce decompositions in Jordan algebras have the usual 0 and 1 eigenspaces
but an additional 1/2-eigenspace emerges as well.

\begin{code}
> Re := (1/2)*(R[1]+R[2]);
> Eigenvalues(Re);
{ <1, 1>, <1/2, 1>, <0, 1> }
\end{code}
\end{example}

\index{ExceptionalJordanCSA}
\begin{intrinsics}
ExceptionalJordanCSA(O) : AlgGen -> AlgGen
ExceptionalJordanCSA(K) : Fld -> AlgGen
\end{intrinsics}

The exception central simple Jordan algebra over the given octonions.
If a field is supplied instead then the split octonion algebra over the
field is used.

\begin{example}[ChevalleyShaferF4]
In characteristic not $2$ or $3$, the exceptional central 
simple Jordan algebra can be used to construct
the exceptional Lie algebra of type $F_4$.

\begin{code}
> J := ExceptionalJordanCSA(Rationals());
> T := Tensor(J);                                     
> T := ChangeTensorCategory(T, HomotopismCategory(3));
> D := DerivationAlgebra(T);
> D2 := Codomain(Induce(D, 2));		// Represent D on U2.
> F4 := D2*D2;			              // Commutator.
> SemisimpleType(F4);
F4
> F4;                             // F4 represented on a 27-dim module.
Matrix Lie Algebra of degree 27 over Rational Field
\end{code}
\end{example}

