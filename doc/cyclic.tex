
\chapterauthor{Peter A.\ Brooksbank}{Bucknell University}

Magma supports similarity testing of modules over cyclic associative rings and cyclic groups.
Module similarity over general rings and groups is graph isomorphism hard.  The algorithms
here are based on \cite{BW:Module-iso}.

\index{IsCyclic}
\begin{intrinsics}
IsCyclic(A) : AlgMat -> BoolElt, AlgAssElt
\end{intrinsics}

Decide if the matrix algebra is generated by a single element, and return such a generator.

\index{IsSimilar}
\begin{intrinsics}
IsSimilar(M, N) : ModRng, ModRng -> BoolElt, Map
\end{intrinsics}

Decides if the given modules are similar; requires that one of the given modules have a cyclic coefficient ring.

\begin{example}[ModuleSimilarity]

In magma modules of a group or algebra are defined by the action of a fixed
generatoring set of the algebra.  Therefore isomorphism of modules in Magma 
assumes the given modules have been specified by the same generating set.
This can lead to a stricter interpretation of isomorphism than perhaps intended
in some situations.  Consider the following example comparing two
1-dimensional vector spaces over the field $GF(9)$.

\begin{code}
> R := MatrixAlgebra(GF(3),2);     
> A := sub<R| [R!1, R![0,1,2,0]]>;
> B := sub<R| [R!1, R![1,1,2,1]]>;
> A eq B;
true
> M := RModule(A); // A 1-dim. GF(9) vector space.
> N := RModule(B); // A 1-dim. GF(9) vector space.
> IsIsomorphic(M,N);
false
> MinimalPolynomial(A.2);
$.1^2 + 1
> MinimalPolynomial(B.2);
$.1^2 + $.1 + 2
\end{code}

Isomorphism of the two modules $M$ and $N$ failed because the two
vector spaces are specified by different generators of $GF(9)$, as confirmed
by the minimum polynomials of the generators.  Module similarity allows
the comparison of modules specified by different generating sets, so
in this example theses to vector spaces can be proven equivalent.

\begin{code}
>IsSimilar(M,N);
true 
[2 0]
[0 2]
\end{code}

Similarity can be used to compare modules given by algebras that are conjugate, but
perhaps not equal.  

\begin{code}
> p := RandomIrreduciblePolynomial(GF(101), 10); 
> q := RandomIrreduciblePolynomial(GF(101), 10); 
> X := CompanionMatrix(p);
> Y := CompanionMatrix(q);
> A := sub<Parent(X)|[X]>;      // Finite field of size 101^10
> B := sub<Parent(Y)|[Y]>;      // Finite field of size 101^10
> M := RModule(A);              // 1-dim vector space over A.
> N := RModule(B);              // 1-dim vector space over B.
> IsIsomorphic(M,N);            // Not isomorphic as A and B are distinct.
false
> cyc, f := IsSimilar(M,N);     // But similar as A is isomorphic to B.
> // f conjugates A into B
> forall { a : a in Generators (A) | f * a * f^-1 in B };
true
> // and f is a semilinear transform M->N.
> forall{ i : i in [1..Ngens (M)] | forall { j : j in [1..Ngens (A)] | \
> (Vector(M.i * A.j) * f) eq (Vector(M.i)*f)*(f^(-1)*A.j*f) } };
true
\end{code}

Similarity is presently available for cyclic algebras.  This can be tested and
a generator returned.

\begin{code}
> M := RandomMatrix(GF(9), 100, 100);
> A := sub< Parent(M) | [ M^(Random(50)) : i in [1..10]] >;
> Ngens(A);
10
> assert IsCyclic(A);
\end{code}
\end{example}

